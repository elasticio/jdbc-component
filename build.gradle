group = 'io.elastic'
apply plugin: 'java'
apply plugin: 'idea'
apply plugin: 'eclipse'
apply plugin: 'groovy'

if (hasProperty('runWithDependencyCheck')) {
    apply plugin: org.owasp.dependencycheck.gradle.DependencyCheckPlugin

    check.dependsOn dependencyCheckAnalyze

    dependencyCheck {
        format = 'HTML'
        failBuildOnCVSS = 7
        suppressionFile='./dependencyCheck-suppression.xml'
    }
}

sourceSets {
    integrationTest {
        java.outputDir = file('build/classes/integrationtest')
        java { srcDir file('src/integration-test/java') }
        resources { srcDir file('src/integration-test/resources') }
    }
    main {
        java.outputDir = file('build/classes/main')
    }
    test {
        java.outputDir = file('build/classes/test')
    }
}

test {
    exclude 'io/elastic/jdbc/integration/**'
    testLogging {
        showStandardStreams = true
    }
    maxParallelForks 4
}

task integrationTest(type: Test) {
    testLogging {
        showStandardStreams = true
    }
    filter() {
        includeTestsMatching "io.elastic.jdbc.integration.*"
    }
}

task downloadDependencies() {
    description 'Download all dependencies to the Gradle cache'
    doLast {
        configurations.findAll { it.canBeResolved }.files
    }
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
    maven {
        url "https://oss.sonatype.org/content/repositories/snapshots"
    }
    mavenCentral()
    mavenLocal()
}

dependencies {
    compile files("./lib/ojdbc6.jar")
    compile files("./lib/sqljdbc4.jar")
    // The following 3 dependencies are to workaround this: https://github.com/elasticio/sailor-jvm/issues/59
    compile 'com.fasterxml.jackson.core:jackson-core:2.10.1'
    compile 'com.fasterxml.jackson.core:jackson-annotations:2.10.1'
    compile 'com.fasterxml.jackson.core:jackson-databind:2.13.2.2'
    compile 'com.google.code.gson:gson:2.9.0'
    compile 'com.microsoft.sqlserver:mssql-jdbc:6.4.0.jre8'
    compile 'io.elastic:sailor-jvm:3.3.9'
    compile 'mysql:mysql-connector-java:8.0.29'
    compile 'org.postgresql:postgresql:42.2.25'

    testCompile 'io.github.cdimascio:java-dotenv:5.1.0'
    testCompile 'org.hsqldb:hsqldb:2.0.0'
    testCompile 'org.spockframework:spock-core:1.1-groovy-2.4'
}

buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'org.owasp:dependency-check-gradle:7.1.0.1'
    }
}

wrapper {
    gradleVersion = '5.4.1'
}
